plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'dev.heypr'
version = '1.2.0-b' + (System.getenv("BUILD_NUMBER") ?: "DEV")

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = "https://repo.bg-software.com/repository/api/"
    }
    maven {
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        url = "https://jitpack.io"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    compileOnly("com.bgsoftware:SuperiorSkyblockAPI:2024.4")
    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.13")
    compileOnly("com.github.angeschossen:LandsAPI:7.15.4")
    implementation project(":api")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
        expand(["BUILD_NUMBER": System.getenv("BUILD_NUMBER")])
    }
}

assemble {
    dependsOn shadowJar
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
    archiveFileName.set("BuildersWand-${project.version}.jar")
}
